apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'checkstyle'

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.neenbedankt.gradle.plugins:android-apt:1.4'
  }
}

android {
  compileSdkVersion 23
  buildToolsVersion "23.0.1"

  defaultConfig {
    applicationId "com.github.glomadrian.wallapopcodetest"
    minSdkVersion 16
    targetSdkVersion 23
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  lintOptions {
    disable 'InvalidPackage'
    abortOnError false
  }

  testOptions {
    unitTests.returnDefaultValues = true
  }
}


//Enable checkstyle task in the project
task checkstyle(type: Checkstyle) {
  configFile file('../config/checkstyle.xml')
  source 'src'
  include '**/*.java'
  exclude '**/gen/**'
  exclude '**/R.java'
  exclude '**/BuildConfig.java'
  exclude '**/*Test.java'

  def configProps = ['proj.module.dir': projectDir.absolutePath]
  configProperties configProps

  classpath = files()
}

// Run checkstyle task on every build
preBuild.dependsOn('checkstyle')
repositories {
  maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:23.0.1'
  apt 'com.google.dagger:dagger-compiler:2.0'
  compile 'com.google.dagger:dagger:2.0'
  provided 'org.glassfish:javax.annotation:10.0-b28'
  compile 'com.jakewharton:butterknife:7.0.1'
  compile 'com.squareup.retrofit:retrofit:1.7.1'
  compile 'com.android.support:recyclerview-v7:23.0.1'
  compile 'com.android.support:cardview-v7:23.0.1'
  compile 'com.squareup.picasso:picasso:2.5.2'
  compile 'com.android.support:design:23.0.1'

  //Unit test dependencies
  testCompile 'junit:junit:4.12'
  testCompile "org.mockito:mockito-core:1.+"

  // Android test dependencies
  androidTestCompile 'com.android.support:support-annotations:23.0.1'
  androidTestCompile 'com.android.support.test:runner:0.4'
  androidTestCompile 'com.android.support.test:rules:0.4'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.1'

  androidTestCompile('com.android.support.test.espresso:espresso-contrib:2.2.1') {
    exclude group: 'com.android.support', module: 'appcompat'
    exclude group: 'com.android.support', module: 'support-v4'
    exclude module: 'recyclerview-v7'
  }
}
